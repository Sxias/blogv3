plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'shop.mtcoding'
version = '1.0'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.3.0'
    implementation group: 'org.qlrm', name: 'qlrm', version: '4.0.1'
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
// 1. gradlew build (test code 실행 -> 결과를 build/generated-snippets에 복사 (*.adoc))
ext {
    set('snippetsDir', file("build/generated-snippets"))
}

// 2. gradlew build (test code 실행 -> 1번 결과 실행)
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}
// 3. 테스트가 종료되면 asciidoctor 실행 : snippet의 .adoc 파일들을 가지고 하나의 .adoc으로 변경 (src/docs/asciidoc)
tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}
// 4. jar 파일 실행되면 (서버 실행) html 파일을 /static/docs로 복사 - 서버에서 열람 가능
bootJar {
    dependsOn asciidoctor
    copy {
        // src/docs/asciidoc == from 경로
        from "${asciidoctor.outputDir}"
        into 'src/main/resources/static/docs'    // /static/docs로 복사!
    }
}

// plain 파일 생성 하지 않기
jar {
    enabled = false
}
